plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'jacoco'
}

group = 'com.github.barbitoff.trading.traderacoon'
version = '0.0.1'

ext['okhttp3.version'] = '4.3.1'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
	implementation 'com.github.barbitoff.trading.traderacoon:traderacoon-trading-api:0.0.1'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.springframework.boot:spring-boot-starter-logging'

    implementation "javax.annotation:javax.annotation-api"

    implementation 'org.mapstruct:mapstruct:1.3.1.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.3.1.Final'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'ru.tinkoff.invest:openapi-java-sdk-core:0.4.1'
    implementation 'ru.tinkoff.invest:openapi-java-sdk-java8:0.4.1'

    implementation 'io.projectreactor:reactor-core:3.3.3.RELEASE'
	
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testImplementation 'org.jeasy:easy-random-core:4.2.0'
    testImplementation "org.mockito:mockito-core:3.3.3"
    //testImplementation "org.answer-it:mock-slf4j:1.0.0"
}

dependencyManagement {
    imports {
        mavenBom('org.springframework.boot:spring-boot-dependencies:2.2.6.RELEASE')
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = rootProject.name
            groupId = group
            from components.java
            pom {
                name = 'Traderacoon Tinkoff Spring Starter'
                description = 'Spring Boot Starter with Traderacoon Trading API implementation for Tinkoff OpeAPI'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
    /*repositories{
        maven {
            credentials {
                username "xxx"
                password "xxx"
            }
            url = "xxx"
        }
    }*/
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn jacocoTestReport
